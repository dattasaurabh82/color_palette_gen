/*
  @Context An appplet to generate color palettes based on an image
 @Location Berlin, Germany
 @author Saurabh Datta (Prophet GMBH)
 @Date May 2024
 */


String FilePath = "";
boolean imgIsFile, imgIsURL;

/* ---- UI related ----- */
import java.awt.Toolkit;
import java.awt.datatransfer.*;
import java.net.*;
import java.util.regex.*;

boolean isMac, isWin, isNix;

String clipboardText = "";
String imgURL = "";
String pastePromptTxt = "";
boolean loadImgFromUrl = false;


import controlP5.*;
ControlP5 cp5;
Textarea myConsole;
int c = 0;
Println console;
boolean debugView = true;

import drop.*;
SDrop drop;

PImage loadImageIcon;
PImage m;

boolean getImgData = false;
int loadingCounter = 0;

int footerHeight = 80;
int padding = 20;
int adjustedAppletWidth = 0;
int adjustedAppletHeight = 0;
int bottomLineY = 0;

int textSizePxL = 24;
int textSizePxM = 18;

String browseStr = "browse";
float browserStrX, browserStrY, browserStrWidth;
boolean overBrowseLink = false;

String clrStr = "CLEAR";
float clrStrX, clrStrY, clrStrWidth;
boolean overClearText = false;

String promptText = "Drop an image here";

color highlightColor = unhex("FF00FFB5");  // Add 'FF' at the beginning for the alpha channel
/* ----------------------- */


/* -- color extraction related -- */
import jto.colorscheme.*;
ColorScheme cs;

color[] colors;
boolean colArrIsNonZero = false;

int heightOfPaletteCell = 64;


import processing.data.JSONObject;
import processing.data.*;
JSONArray palette;
/* ------------------------------ */


void setup() {
    // Applet setup
    size(640, 480);
    background(0);

    detectOs();
    if (isMac || isNix) {
        pastePromptTxt = "or\npaste an image URL (cmd+v)";
    } else if (isWin) {
        pastePromptTxt = "or\npaste an image URL (ctrl+v)";
    }

    cp5 = new ControlP5(this);
    cp5.enableShortcuts();

    myConsole = cp5.addTextarea("txt")
        .setPosition(5, 0)
        .setSize(300, 250)
        .setFont(createFont("", 9))
        .setLineHeight(12)
        .setColor(color(255))
        .setColorBackground(color(0, 150))
        .setColorForeground(color(255));
    ;
    if (debugView) {
        myConsole.show();
    } else {
        myConsole.hide();
    }
    console = cp5.addConsole(myConsole);

    // Inititlaize color palette array to zeros
    colors = new color[0];

    loadImageIcon = loadImage("img_icon_mint.png");
    loadImageIcon.resize(0, 75);

    // Calculations for image positioning in the applet (after it has loaded)
    adjustedAppletWidth = width - padding * 2;
    bottomLineY = height - footerHeight;
    adjustedAppletHeight = bottomLineY - padding;

    // "File drop" object initialization
    drop = new SDrop(this);

    smooth();
}


void draw () {
    background(0);

    // image laoding method for Drop event and File selection
    if (m != null && m.width > 0) {
        resizeAndDisplayImg(m);       // resizes and center places the loaded image 
        displayClearImageText();      // user prompt as "clear" the image
        displayColorPalette(colors);  // shows the generated color scheme/palettes at the bottom
    } else {
        displayImgLoadPrompt();       // shows prompt for user to "drop a file"
    }

    // image loading method for Pasted URL
    if (loadImgFromUrl && clipboardText != null && clipboardText.length() > 0) {
        loadImgFromUrl = false;
        // 1. download the image in Data folder with a specific name
        m = null;       // set the img object to null
        FilePath = "";  // reste FilePath
        getImgData = false;
        m = loadImage(clipboardText, "png"); // then load the img (blocking)
        m.save("data/webImg.png");
        // 2. Load the image now
        m = loadImage("webImg.png");
        // 3. assign it to the global var "FilePath" for the palette generator
        FilePath = sketchPath() + "/data/webImg.png";
        // 4. Tell the sketch to rpoceed to resizing, etc. now
        getImgData = true;
    }

    // Visual divider for Lower "control area" (footer section) of the applet
    strokeWeight(0.5);
    stroke(255);
    line(0, adjustedAppletHeight, width, adjustedAppletHeight);

    // keyboard shortcut info on screen
    textAlign(CENTER);
    textSize(12);
    fill(255);
    text("L: Load Img,  P: Create Palette,   C: Clear Img,   D: Toggle Debug View,  0: Clear Console", width/2, height-8);
}


void keyPressed(KeyEvent event) {
    if (key == 'l' || key == 'L') {
        selectInput("Select an image", "fileSelected");
    }

    if (key == 'c' || key == 'C') {
        resetParameters();
    }

    if (key == 'p' || key == 'P') {
        generatePalette(FilePath);
    }

    // for console
    if (key == 'd' || key == 'D') {
        debugView = !debugView;
        if (debugView) {
            myConsole.show();
        } else {
            myConsole.hide();
        }
    }
    if (key == '0') {
        console.clear();
    }

    // Check if Ctrl+V or Cmd+V is pressed
    if ((key == 'v' || key == 'V') && (event.isControlDown() || event.isMetaDown())) {
        // Access the system clipboard
        Clipboard clipboard = Toolkit.getDefaultToolkit().getSystemClipboard();
        Transferable contents = clipboard.getContents(null);

        // Check if clipboard contains text
        if (contents != null && contents.isDataFlavorSupported(DataFlavor.stringFlavor)) {
            try {
                // Get the text from the clipboard
                clipboardText = (String)contents.getTransferData(DataFlavor.stringFlavor);
                print("Pasted String: ");
                println(clipboardText);
                print("Is Img URL: ");
                println(isImageUrl(clipboardText));

                if (isImageUrl(clipboardText)) {
                    loadImgFromUrl = true;
                } else {
                    loadImgFromUrl = false;
                }
            } 
            catch (Exception e) {
                e.printStackTrace();
            }
        }
    }
}


void mousePressed() {
    if (overBrowseLink) {
        selectInput("Select an image", "fileSelected");
    }
    if (overClearText) {
        resetParameters();
    }
}

void mouseMoved() {
    checkOverText();
}

void mouseDragged() {
    checkOverText();
}


void resetParameters() {
    println("\nclearing up image & reseting things ...");
    m = clearImg(m);          // make the image object null
    FilePath = "";            // reset the file Path global var
    cs = null;                // reset the color scheme object to null
    colors = new color[0];    // re-initialize color palette array to all zeros
    colArrIsNonZero = false;  //
}

void checkOverText() {
    // line(width/2-browserStrWidth/2, browserStrY+2, width/2+browserStrWidth/2, browserStrY+2);
    if (mouseX >= width/2-browserStrWidth/2 && mouseX <= width/2+browserStrWidth/2 && mouseY >= browserStrY-10 && mouseY <= browserStrY+2) {
        overBrowseLink = true;
    } else {
        overBrowseLink = false;
    }

    if (m != null) {
        //  text(clrString, width-(clrStringWidth+10), adjustedAppletHeight-5);
        if (mouseX >= clrStrX && mouseX <= clrStrX+clrStrWidth && mouseY >= clrStrY-20 && mouseY <= clrStrY) {
            overClearText = true;
        } else {
            overClearText = false;
        }
    } else {
        overClearText = false;
    }
}


PImage clearImg(PImage img) {
    if (img == null) {
        println("There was no image to be cleared ...");
        return null;
    } else {
        img = null;
        System.gc(); // garbage collection
        println("Image cleared up!");
        return img;
    }
}



// Display Prompt for user to drop image
void displayImgLoadPrompt() {
    noStroke();
    fill(30);
    rectMode(CENTER);
    rect(width/2, height/2 - footerHeight/2-10, width/2+50, width/2);
    imageMode(CENTER);
    image(loadImageIcon, width/2, height/2-125);
    fill(255);
    textSize(textSizePxL);
    textAlign(CENTER);
    text(promptText, width/2, height/2 - footerHeight/2-textSizePxL/2);
    textSize(textSizePxM);
    text("or", width/2, height/2 - footerHeight/2+textSizePxL/2);
    if (overBrowseLink) {
        fill(highlightColor);
        stroke(highlightColor);
    } else {
        fill(255);
        stroke(255);
    }
    browserStrWidth = textWidth(browseStr);
    browserStrX = width/2;
    browserStrY = height/2 - footerHeight/2+(textSizePxL/2*3);
    text(browseStr, browserStrX, browserStrY);
    strokeWeight(1);
    line(width/2-browserStrWidth/2, browserStrY+2, width/2+browserStrWidth/2, browserStrY+2);

    fill(255);
    text(pastePromptTxt, width/2, height/2 - footerHeight/2+(textSizePxL*3));

    noFill();
}


// Display Prompt for user to clear the image
void displayClearImageText() {
    if (overClearText) {
        fill(highlightColor);
    } else {
        fill(150);
    }
    textAlign(LEFT);
    textSize(14);

    clrStrWidth = textWidth(clrStr);
    clrStrX = width-(clrStrWidth+12);
    clrStrY = adjustedAppletHeight-5;
    text(clrStr, clrStrX, clrStrY);
}



// Callback for detecting drop event of image file
public void dropEvent(DropEvent theDropEvent) {
    // Check if the dropped object is an image and if so, load it
    if (theDropEvent.isImage()) {
        m = theDropEvent.loadImage();
        // Once ensured that this file is an image,
        // assign it to the global var "FilePath" for the palette generator
        FilePath = theDropEvent.file().getAbsolutePath();
        // println(FilePath);
        getImgData = true;   // this var is used to ensure that image has been loaded now grab info to do resizing if needed

        imgIsFile = true;
        imgIsURL = false;

        println("\nLoading image ...");
    } else {
        // show user that it wasn't an image
        promptText = "\nNot image! Try again!";
        println(promptText);

        imgIsFile = false;
        imgIsURL = false;
    }
}


// Callback for window based image file selection (image file)
public void fileSelected(File selection) {
    if (selection != null) {
        // check it is an image or not
        if (selectionIsImage(selection)) {
            // Once ensured that this file is an image,
            m = loadImage(selection.getAbsolutePath());
            // assign it to the global var "FilePath" for the palette generator
            FilePath = selection.getAbsolutePath();
            getImgData = true;   // this var is used to ensure that image has been loaded now grab info to do resizing if needed

            imgIsFile = true;
            imgIsURL = false;

            println("\nLoading image ...");
        } else {
            // show user that it wasn't an image
            promptText = "\nNot image! Try again!";
            println(promptText);

            imgIsFile = false;
            imgIsURL = false;
        }
    } else {
        println("Image selection was cancelled! Try again?");

        imgIsFile = false;
        imgIsURL = false;
    }
}



// Custom function to check file type 
public boolean selectionIsImage(File file) {
    String[] imgExtensions = {"jpg", "jpeg", "png", "gif", "bmp"};
    String fileName = file.getName();
    String extension = fileName.substring(fileName.lastIndexOf('.') + 1).toLowerCase();
    for (String ext : imgExtensions) {
        if (extension.equals(ext)) {
            return true;
        }
    }
    return false;
}



// Display function of image and upon when the image has been dropped
public void resizeAndDisplayImg(PImage img) {
    if (getImgData) {
        // ** We are using a counter, to pass few cycles, as the image doesn't get loaded immediately
        loadingCounter++;
        if (loadingCounter == 20) {
            println("orig img w:", img.width, " orig img h:", img.height);
            // Business logic for resizing image
            if (img.width > adjustedAppletWidth || img.height > adjustedAppletHeight) {
                // Check if the image is larger than the adjusted dimensions
                float aspectRatio = float(img.width) / img.height;  // Calculate aspect ratio of the image

                if (aspectRatio > 1) {
                    // Image is wider than tall
                    img.resize(adjustedAppletWidth, 0);  // Resize width to fit adjusted width, height will be auto-calculated
                } else {
                    // Image is taller than wide or square
                    img.resize(0, adjustedAppletHeight);  // Resize height to fit adjusted height, width will be auto-calculated
                }
                println("resized img w:", img.width, " resized img h:", img.height);
            } else {
                println("No resizing needed ...");
            }
            loadingCounter = 0;
            getImgData = false;
        }
    }
    // Draw the image
    imageMode(CORNER);
    image(img, (width - m.width) / 2, adjustedAppletHeight/2-img.height/2);
}


void generatePalette(String filePath) {
    // check if the file exists
    File f = new File(filePath);
    if (f.exists()) {
        println("The file exists.");
        println(filePath);
        println("Preparing color palette ...");
        try {
            // create our color scheme object
            cs = new ColorScheme(filePath, this);
            // get the list of colors from the color scheme
            colors = cs.toArray();
            println("Color palette (and scheme) prepared!");
            println("\nYou can now cycle through schemes");

            // Create a JSONArray to store color objects
            palette = new JSONArray();

            // Print RGB values for each color
            for (int i = 0; i < colors.length; i++) {
                color c = colors[i];
                String ch = hex(c);
                int r = int(red(c));
                int g = int(green(c));
                int b = int(blue(c));

                println(String.valueOf(r) + ", " + String.valueOf(g) + ", " + String.valueOf(b) + ", " + ch);

                // Create a JSONObject for each color
                JSONObject colorObj = new JSONObject();
                colorObj.setInt("r", r);
                colorObj.setInt("g", g);
                colorObj.setInt("b", b);
                colorObj.setString("hex", ch);

                // Add the color object to the palette array
                palette.setJSONObject(i, colorObj);
            }

            // Print the JSON object
            // println(palette);

            // Check if the palette array is not empty
            println("");
            if (palette.size() > 0) {
                // Convert the JSON array to a string
                String jsonString = palette.toString();
                try {
                    // Save the JSON string to the sketch's data folder
                    saveStrings("data/curr_palette.json", new String[]{jsonString});
                    println("Palette JSON object saved successfully.");
                }
                catch(Exception e) {
                    println("Palette JSON object could not be writtent o a file");
                }
            } else {
                println("Palette JSON object is empty. Not saved.");
            }
        } 
        catch (OutOfMemoryError e) {
            // Handle the OutOfMemoryError
            println("Ran out of memory!\nImage too big and the respectivce color array is LAAARRGEEE ...");
            resetParameters();
            // You can add more error handling code here
        } 
        catch (Exception e) {
            // Handle other exceptions
            e.printStackTrace();
            resetParameters();
        }
    } else {
        println("The image file does not exist.");
        println("Can't proceed for palette Generation ...");
    }
}



void displayColorPalette(color[] colors) {
    // check if the color is not black
    for (color c : colors) {
        if (c != color(0, 0, 0)) { 
            colArrIsNonZero = true;
            break;
        }
    }

    // if color scheme object is not null and if all the col elements in the the color array are non-zeros  
    if (cs != null && colArrIsNonZero) {
        for (int i = 0; i < colors.length; i++) {
            // println(hex(colors[i]));
            rectMode(CORNER);
            int widthOfCell = width/colors.length;
            int x = i*widthOfCell;

            strokeWeight(0.5);
            stroke(highlightColor);
            fill(colors[i]);
            rect(x, adjustedAppletHeight, x + widthOfCell, heightOfPaletteCell);

            if (debugView) {
                // Show the RGB on top of the color palettes
                color c = colors[i];
                String ch = hex(c);
                String r = String.valueOf(int(red(c)));
                String g = String.valueOf(int(green(c)));
                String b = String.valueOf(int(blue(c)));
                String dvColorStr = r + "," + g + "," + b;
                textSize(9);
                textAlign(CENTER);
                fill(255); // ** Adjust later based on rect color [TBD]
                text(dvColorStr, x+widthOfCell/2, adjustedAppletHeight+heightOfPaletteCell/2);
                text(ch, x+widthOfCell/2, (adjustedAppletHeight+heightOfPaletteCell/2)+10);
            }
        }
    }
}


boolean isImageUrl(String urlString) {
    try {
        URL url = new URL(urlString);
        String file = url.getFile();

        // Define regular expression pattern to match common image file extensions
        Pattern pattern = Pattern.compile("\\.(jpg|jpeg|png|gif|bmp)$", Pattern.CASE_INSENSITIVE);
        Matcher matcher = pattern.matcher(file);

        // Check if URL ends with a known image file extension
        if (matcher.find()) {
            return true;
        }

        // Check if URL contains image-related query parameters
        String query = url.getQuery();
        if (query != null && (query.contains("format=image") || query.contains("auto=format"))) {
            return true;
        }

        // Check if URL contains a known image path
        if (file.endsWith("/photo") || file.contains("/photos/") || file.contains("/images/")) {
            return true;
        }

        // download the image
        // TBD []

        imgIsFile = false;
        imgIsURL = true;

        return false;
    } 
    catch (MalformedURLException e) {
        imgIsFile = false;
        imgIsURL = false;

        // URL is not valid
        return false;
    }
}


void detectOs() {
    if (platform == PConstants.WINDOWS) {
        isMac = false;
        isWin = true;
        isNix = false;
    } else if (platform == PConstants.MACOSX) {
        isMac = true;
        isWin = false;
        isNix = false;
    } else if (platform == PConstants.LINUX) {
        isMac = false;
        isWin = false;
        isNix = true;
    }
}
